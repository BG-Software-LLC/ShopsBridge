plugins {
    id 'java'
    id 'io.github.goooler.shadow' version '8.1.8'
    id 'maven-publish'
}

java {
    withSourcesJar()
}

group 'ShopsBridge'

project.ext {
    targetFolder = file("target/")
    buildVersion = System.getenv("BUILD_NUMBER") == null ? "Unknown" : "b" + System.getenv("BUILD_NUMBER")
}

allprojects {
    apply plugin: 'java'
    apply plugin: 'io.github.goooler.shadow'

    java {
        toolchain {
            languageVersion.set(JavaLanguageVersion.of(8))
        }
    }

    repositories {
        maven { url 'https://repo.bg-software.com/repository/nms/' }
        maven { url 'https://repo.bg-software.com/repository/dependencies/' }
        maven { url 'https://repo.bg-software.com/repository/common/' }
    }

    dependencies {
        compileOnly 'com.bgsoftware.common.annotations:Annotations:b2'
    }

    task checkDebug() {
        Set<File> filesWithDebug = fileTree('src/main/java').filter { file ->
            file.text.contains('Bukkit.broadcastMessage')
        }.getFiles()

        if (!filesWithDebug.isEmpty())
            throw new GradleException("Found debug messages: " + filesWithDebug)
    }

    build {
        dependsOn checkDebug
        dependsOn shadowJar
    }
}

repositories {
    mavenCentral()
}

dependencies {
    implementation subprojects

    compileOnly "org.spigotmc:v1_8_R3-Taco:latest"
}

shadowJar {
    dependsOn(jar)

    archiveFileName = rootProject.name + "-" + rootProject.buildVersion + ".jar"

    delete fileTree(rootProject.targetFolder.getAbsolutePath())

    exclude '*exclude.jar'

    destinationDirectory = rootProject.targetFolder
    from sourceSets.getByName("main").output
    configurations = [project.configurations.getByName("runtimeClasspath")]
}

build {
    dependsOn shadowJar
}

publishing {
    publications {
        maven(MavenPublication) {
            groupId = 'com.bgsoftware.common.shopsbridge'
            artifactId = 'ShopsBridge'
            version = rootProject.buildVersion

            from components.java
        }
    }

    repositories {
        String mavenUsername = System.getenv('mavenUsername');
        String mavenPassword = System.getenv('mavenPassword');

        if (mavenUsername != null && mavenPassword != null) {
            maven {
                url 'https://repo.bg-software.com/repository/common/'
                credentials {
                    username mavenUsername
                    password mavenPassword
                }
            }
        }
    }
}

publish.shouldRunAfter shadowJar
shadowJar.shouldRunAfter build
build.shouldRunAfter subprojects.build